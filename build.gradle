plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
    id 'java'
}

subprojects {
    apply plugin: 'base'
    apply plugin: 'java'
    apply plugin: 'com.palantir.docker'

    ext {
        sharedDir                     = file("${project.rootDir}/shared")
        baseSailfishVersion           = '3.3'
        sailfishVersion               = "${baseSailfishVersion}.51"

        pluginMainDir                 = file('src/main/plugin')
        pluginGenDir                  = file('src/gen/plugin')
        versionFile                   = file("${pluginGenDir}/VERSION")
        dockerSailfishWorkspaceDir    = 'sailfish/workspace/plugins/th2_service'
        dockerSailfishWorkspaceLibDir = "${dockerSailfishWorkspaceDir}/libs"
    }

    ext.excludeSailfish = { rcd ->
        rcd.excludeModule("com.exactpro.sf", "sailfish-core")
        rcd.excludeModule("com.exactpro.sf", "sailfish-common")
        rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
        rcd.excludeModule("com.exactpro.sf", "service-http")
        rcd.excludeModule("com.exactpro.sf", "service-tcpip")
        rcd.excludeModule("com.exactpro.sf", "service-fix")
        rcd.excludeModule("com.exactpro.sf", "service-ntg")
        rcd.excludeModule("com.exactpro.sf", "service-itch")
        rcd.excludeModule("com.exactpro.sf", "service-soup")
    }


    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }

        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            content {
                excludeSailfish(it)
            }
        }
        // ignoreGradleMetadataRedirection is used for sonatype because
        // Sailfish dependencies have constrains that interfere with our BOM
        // so we exclude Gradle metadata for this repositories.
        // We've checked these versions - they are compatible and safe to use
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
            content {
                excludeSailfish(it)
            }
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }
        mavenCentral()
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations {
        service
    }

    dependencies {
        service ("com.exactpro.sf:${sf_service_name}:${sailfishVersion}") {
            exclude module: 'sailfish-core'
            exclude module: 'sailfish-common'
        }
        service ('com.fasterxml.jackson.core:jackson-databind:2.14.1')

    }

    task pluginWriteVersion {
        outputs.file versionFile
        doLast{
            versionFile.write "lightweight: true\n" +
                              "plugin_alias: th2_service\n" +
                              "name: th2_service\n" +
                              "build_number: 0\n" +
                              "revision: 0\n" +
                              "git_hash: 0\n" +
                              "branch: fake\n" +
                              "version: ${baseSailfishVersion}.0.0\n" +
                              "core_version: ${baseSailfishVersion}.0"
        }
    }

    dockerPrepare {
        dependsOn pluginWriteVersion
    }

    docker {
        copySpec.with {
            from(configurations.service) {
                into dockerSailfishWorkspaceLibDir
            }
            from(pluginMainDir) {
                into dockerSailfishWorkspaceDir
            }
            from(pluginGenDir) {
                into dockerSailfishWorkspaceDir
            }
        }
    }

    clean {
        delete(pluginGenDir)
    }
}
